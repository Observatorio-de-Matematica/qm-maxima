\input texinfo   @c -*-texinfo-*-

@setfilename qm.info
@settitle Package qm

@ifinfo
@macro var {expr}
<\expr\>
@end macro
@end ifinfo

@dircategory Mathematics/Maxima
@direntry
* Package qm: (maxima) Maxima share package qm for quantum mechanics
@end direntry

@menu
* Introduction to package qm::
* Functions and Variables for qm::
@end menu
@node Top, Introduction to package qm, (dir), (dir)
@top

@menu
* Introduction to package qm::
* Functions and Variables for qm::

@detailmenu
 --- The Detailed Node Listing ---

* Introduction to package qm::
* Functions and Variables for qm::

@end detailmenu
@end menu


@chapter Package qm

@node Introduction to package qm, Functions and Variables for qm, Top, Top
@section Introduction to package qm

The @code{qm} package was written by Eric Majzoub, University of
Missouri. Email: majzoube-at-umsystem.edu

The package is loaded with: @code{load(qm);}

The @code{qm} package provides functions and standard definitions to
solve quantum mechanics problems in a finite dimensional Hilbert
space. For example, one can calculate the outcome of Stern-Gerlach
experiments using built-in definitions of the Sx, Sy, and Sz operators
for arbitrary spin, e.g. @code{s=@{1/2, 1, 3/2, @dots{}@}}. For spin-1/2
the standard basis kets in the @code{x}, @code{y}, and @code{z}-basis
are available as @code{@{xp,xm@}}, @code{@{yp,ym@}}, and
@code{@{zp,zm@}}, respectively. One can create general ket vectors with
arbitrary but finite dimension and perform standard computations such as
expectation value, variance, etc. The angular momentum @var{|j,m>}
representation of kets is also available. Tensor product states for
multiparticle systems can be created to perform calculations on those
systems.

Let us consider a simple example involving spin-1/2 particles.
A bra vector in the @code{z}-basis may be written as 

@ @ @ @ @ @ @ @ @code{<psi| = a <z+| + b <z-|}.

The bra @code{<psi|} will be represented in Maxima by the row vector
@code{[a b]}, where the basis vectors are

@ @ @ @ @ @ @ @ @code{<z+| = [1 0]}

@noindent
and

@ @ @ @ @ @ @ @ @code{<z-| = [0 1]}.

In a Maxima session this looks like the following. The basis kets
@code{@{zp,zm@}} are transformed into bras using the @code{dagger}
function.

@example
@group
(%i1) psi_bra:a*dagger(zp)+b*dagger(zm);
(%o1)                              [ a  b ]
@end group
@end example


@subsection Types of kets and bras

There are two types of kets and bras available in the @code{qm} package,
the first type is given by a @emph{matrix representation}, as returned
by the functions @code{mbra} and @code{mket}. @code{mket}s are column
vectors and @code{mbra}s are row vectors, and their components are
entered as Maxima @emph{lists} in the @code{mbra} and @code{mket}
functions. The second type of bra or ket is @emph{abstract}; there is no
matrix representation. Abstract bras and kets are entered using the
@code{bra} and @code{ket} functions, while also using Maxima lists for
the elements. These general kets are displayed in Dirac
notation. Abstract bras and kets are used for both the @code{(j,m)}
representation of states and also for tensor products. For example, a
tensor product of two ket vectors @code{|a>} and @code{|b>} is input as
@code{ket([a,b])} and displayed as

@ @ @ @ @ @ @ @ @code{|[a,b]>} @ @ @ @ @ (general ket)

@noindent
Note that abstract kets and bras are @emph{assumed to be
orthonormal}. These general bras and kets may be used to build
arbitrarily large tensor product states.

The following examples illustrate some of the basic capabilities of the
@code{qm} package. Here both abstract, and concrete (matrix
representation) kets are shown. The last example shows how to contstruct
an entangled Bell pair.

@example
@group
(%i1) ket([a,b])+ket([c,d]);
(%o1)                           |c, d> + |a, b>
(%i2) mket([a,b]);
                                     [ a ]
(%o2)                                [   ]
                                     [ b ]
(%i3) mbra([a,b]);
(%o3)                              [ a  b ]
(%i4) bell:(1/sqrt(2))*(ket([u,d])-ket([d,u]));
                                |u, d> - |d, u>
(%o4)                           ---------------
                                    sqrt(2)
(%i5) dagger(bell);
                                <u, d| - <d, u|
(%o5)                           ---------------
                                    sqrt(2)
@end group
@end example

Note that @code{ket([a,b])} is treated as tensor product of states
@code{a} and @code{b} as shown below.

@example
@group
(%i1) braket(bra([a1,b1]),ket([a2,b2]));
(%o1)                kron_delta(a1, a2) kron_delta(b1, b2)
@end group
@end example

Constants that multiply kets and bras must be declared complex by
the user in order for the dagger function to properly conjugate
such constants. The example below illustrates this behavior.

@example
@group
(%i1) declare([a,b],complex);
(%o1)                                done
(%i2) psi:a*ket([1])+b*ket([2]);
(%o2)                            |2> b + |1> a
(%i3) psidag:dagger(psi);
(%o3)                 <2| conjugate(b) + <1| conjugate(a)
(%i4) psidag . psi;
(%o4)                   b conjugate(b) + a conjugate(a)
@end group
@end example

The following shows how to declare a ket with both real and
complex components in the matrix representation.

@example
@group
(%i1) declare([c1,c2],complex,r,real);
(%o1)                                done
(%i2) k:mket([c1,c2,r]);
                                    [ c1 ]
                                    [    ]
(%o2)                               [ c2 ]
                                    [    ]
                                    [ r  ]
(%i3) b:dagger(k);
(%o3)                 [ conjugate(c1)  conjugate(c2)  r ]
(%i4) b . k;
                    2
(%o4)              r  + c2 conjugate(c2) + c1 conjugate(c1)
@end group
@end example

@node Functions and Variables for qm, , Introduction to package qm, Top
@section Functions and Variables for qm

@defvr {Variable} hbar
Planck's constant divided by @code{2*%pi}. @code{hbar} is not given a
floating point value, but is declared to be a real number greater than
zero.
@end defvr

@anchor{ket}
@deffn {Function} ket ([@code{k@sub{1}},@code{k@sub{2}},@dots{}])
@code{ket} creates a general state ket, or tensor product, with symbols
@code{k@sub{i}} representing the states. The state kets @code{k@sub{i}}
are assumed to be orthonormal.
@end deffn

@example
@group
(%i1) k:ket([u,d]);
(%o1)                               |u, d>
(%i2) b:bra([u,d]);
(%o2)                               <u, d|
(%i3) b . k;
(%o3)                                  1
@end group
@end example

@anchor{ketp}
@deffn {Function} ketp (abstract ket)
@code{ketp} is a predicate function for abstract kets. It returns
@code{true} for abstract @code{ket}s and @code{false} for anything else.
@end deffn

@anchor{bra}
@deffn {Function} bra ([@code{b@sub{1}},@code{b@sub{2}},@dots{}])
@code{bra} creates a general state bra, or tensor product, with symbols
@code{b@sub{i}} representing the states. The state bras @code{b@sub{i}}
are assumed to be orthonormal.
@end deffn

@example
@group
(%i1) k:ket([u,d]);
(%o1)                               |u, d>
(%i2) b:bra([u,d]);
(%o2)                               <u, d|
(%i3) b . k;
(%o3)                                  1
@end group
@end example

@anchor{brap}
@deffn {Function} brap (abstract bra)
@code{brap} is a predicate function for abstract bras. It returns
@code{true} for abstract @code{bra}s and @code{false} for anything else.
@end deffn

@anchor{mket}
@deffn {Function} mket ([@code{c@sub{1}},@code{c@sub{2}},@dots{}])
@code{mket} creates a @emph{column} vector of arbitrary finite
dimension. The entries @code{c@sub{i}} can be any Maxima expression.
The user must @code{declare} any relevant constants to be complex. For a
matrix representation the elements must be entered as a list in
@code{[@dots{}]} square brackets.
@end deffn

@example
@group
(%i1) declare([c1,c2],complex);
(%o1)                                done
(%i2) mket([c1,c2]);
                                    [ c1 ]
(%o2)                               [    ]
                                    [ c2 ]
(%i3) facts();
(%o3) [kind(hbar, real), hbar > 0, kind(c1, complex), kind(c2, complex)]
@end group
@end example

@anchor{mketp}
@deffn {Function} mketp (@emph{ket})
@code{mketp} is a predicate function that checks if its input is an mket,
in which case it returns @code{true}, else it returns @code{false}.
@code{mketp} only returns @code{true} for the matrix representation of a ket.
@end deffn

@example
@group
(%i1) k:ket([a,b]);
(%o1)                               |a, b>
(%i2) mketp(k);
(%o2)                                false
(%i3) k:mket([a,b]);
                                     [ a ]
(%o3)                                [   ]
                                     [ b ]
(%i4) mketp(k);
(%o4)                                true
@end group
@end example

@anchor{mbra}
@deffn {Function} mbra ([@code{c@sub{1}},@code{c@sub{2}},@dots{}])
@code{mbra} creates a @emph{row} vector of arbitrary finite
dimension. The entries @code{c@sub{i}} can be any Maxima expression.
The user must @code{declare} any relevant constants to be complex.
For a matrix representation the elements must be entered as a list
in @code{[@dots{}]} square brackets.
@end deffn

@example
@group
(%i1) kill(c1,c2);
(%o1)                                done
(%i2) mbra([c1,c2]);
(%o2)                             [ c1  c2 ]
(%i3) facts();
(%o3)                    [kind(hbar, real), hbar > 0]
@end group
@end example

@anchor{mbrap}
@deffn {Function} mbrap (@emph{bra})
@code{mbrap} is a predicate function that checks if its input is an mbra,
in which case it returns @code{true}, else it returns @code{false}.
@code{mbrap} only returns @code{true} for the matrix representation of a bra.
@end deffn

@example
@group
(%i1) b:mbra([a,b]);
(%o1)                              [ a  b ]
(%i2) mbrap(b);
(%o2)                                true
@end group
@end example

Two additional functions are provided to create kets and bras in the
matrix representation. These functions conveniently attempt to
automatically @code{declare} constants as complex. For example, if a
list entry is @code{a*sin(x)+b*cos(x)} then only @code{a} and @code{b}
will be @code{declare}-d complex and not @code{x}.

@anchor{autoket}
@deffn {Function} autoket ([@code{a@sub{1},a@sub{2},@dots{}}])
@code{autoket} takes a list [@code{a@sub{1},a@sub{2},@dots{}}] and
returns a ket with the coefficients @code{a@sub{i}} @code{declare}-d
complex. Simple expressions such as @code{a*sin(x)+b*cos(x)} are allowed
and will @code{declare} only the coefficients as complex.
@end deffn

@example
@group
(%i1) autoket([a,b]);
                                     [ a ]
(%o1)                                [   ]
                                     [ b ]
(%i2) facts();
(%o2)  [kind(hbar, real), hbar > 0, kind(a, complex), kind(b, complex)]
@end group
@group
(%i1) autoket([a*sin(x),b*sin(x)]);
                                 [ a sin(x) ]
(%o1)                            [          ]
                                 [ b sin(x) ]
(%i2) facts();
(%o2)  [kind(hbar, real), hbar > 0, kind(a, complex), kind(b, complex)]
@end group
@end example

@anchor{autobra}
@deffn {Function} autobra ([@code{a@sub{1},a@sub{2},@dots{}}])
@code{autobra} takes a list [@code{a@sub{1},a@sub{2},@dots{}}] and
returns a bra with the coefficients @code{a@sub{i}} @code{declare}-d
complex. Simple expressions such as @code{a*sin(x)+b*cos(x)} are allowed
and will @code{declare} only the coefficients as complex.
@end deffn

@example
@group
(%i1) autobra([a,b]);
(%o1)                              [ a  b ]
(%i2) facts();
(%o2)  [kind(hbar, real), hbar > 0, kind(a, complex), kind(b, complex)]
@end group
@group
(%i1) autobra([a*sin(x),b]);
(%o1)                           [ a sin(x)  b ]
(%i2) facts();
(%o2)  [kind(hbar, real), hbar > 0, kind(a, complex), kind(b, complex)]
@end group
@end example

@anchor{dagger}
@deffn {Function} dagger (@emph{vector})
@code{dagger} is the quantum mechanical @emph{dagger} function and returns
the @code{conjugate} @code{transpose} of its input. Arbitrary constants
must be @code{declare}-d complex for dagger to produce the conjugate.
@end deffn

@example
@group
(%i1) dagger(mbra([%i,2]));
                                   [ - %i ]
(%o1)                              [      ]
                                   [  2   ]
@end group
@end example

@anchor{braket}
@deffn {Function} braket (@code{psi,phi})
Given two kets @code{psi} and @code{phi}, @code{braket} returns the
quantum mechanical bracket @code{<psi|phi>}. The vector @code{psi} may be
input as either a @code{ket} or @code{bra}. If it is a @code{ket} it will be
turned into a @code{bra} with the @code{dagger} function before the inner
product is taken. The vector @code{phi} must always be a @code{ket}.
@end deffn

@example
@group
(%i1) declare([a,b,c],complex);
(%o1)                                done
(%i2) braket(mket([a,b,c]),mket([a,b,c]));
(%o2)          c conjugate(c) + b conjugate(b) + a conjugate(a)
(%i3) braket(ket([a1,b1,c1]),ket([a2,b2,c2]));
(%o3)      kron_delta(a1, a2) kron_delta(b1, b2) kron_delta(c1, c2)
@end group
@end example

@anchor{norm}
@deffn {Function} norm (@code{psi})
Given a @code{ket} or @code{bra} @code{psi}, @code{norm} returns the
square root of the quantum mechanical bracket @code{<psi|psi>}.
The vector @code{psi} must always be a @code{ket}, otherwise the
function will return @code{false}.
@end deffn

@example
@group
(%i1) declare([a,b,c],complex);
(%o1)                                done
(%i2) norm(mket([a,b,c]));
(%o2)       sqrt(c conjugate(c) + b conjugate(b) + a conjugate(a))
@end group
@end example

@deffn {Function} magsqr (@code{c})
@code{magsqr} returns @code{conjugate(c)*c}, the magnitude
squared of a complex number.
@end deffn

@example
@group
(%i1) declare([a,b,c,d],complex);
(%o1)                                done
(%i2) A:braket(mket([a,b]),mket([c,d]));
(%o2)                   conjugate(b) d + conjugate(a) c
(%i3) P:magsqr(A);
(%o3) (conjugate(b) d + conjugate(a) c) (b conjugate(d) + a conjugate(c))
@end group
@end example

@subsection Spin-1/2 state kets and associated operators

Spin-1/2 particles are characterized by a simple 2-dimensional Hilbert
space of states. It is spanned by two vectors. In the @var{z}-basis
these vectors are @code{@{zp,zm@}}, and the basis kets in the
@var{z}-basis are @code{@{xp,xm@}} and @code{@{yp,ym@}} respectively.

@deffn {Function} zp
Return the @var{|z+>} ket in the @var{z}-basis.
@end deffn

@deffn {Function} zm
Return the @var{|z->} ket in the @var{z}-basis.
@end deffn

@deffn {Function} xp
Return the @var{|x+>} ket in the @var{z}-basis.
@end deffn

@deffn {Function} xm
Return the @var{|x->} ket in the @var{z}-basis.
@end deffn

@deffn {Function} yp
Return the @var{|y+>} ket in the @var{z}-basis.
@end deffn

@deffn {Function} ym
Return the @var{|y->} ket in the @var{z}-basis.
@end deffn

@example
@group
(%i1) zp;
                                     [ 1 ]
(%o1)                                [   ]
                                     [ 0 ]
(%i2) zm;
                                     [ 0 ]
(%o2)                                [   ]
                                     [ 1 ]
@end group
@group
(%i1) yp;
                                  [    1    ]
                                  [ ------- ]
                                  [ sqrt(2) ]
(%o1)                             [         ]
                                  [   %i    ]
                                  [ ------- ]
                                  [ sqrt(2) ]
(%i2) ym;
                                 [     1     ]
                                 [  -------  ]
                                 [  sqrt(2)  ]
(%o2)                            [           ]
                                 [     %i    ]
                                 [ - ------- ]
                                 [   sqrt(2) ]
@end group
@group
(%i1) braket(xp,zp);
                                       1
(%o1)                               -------
                                    sqrt(2)
@end group
@end example

Switching bases is done in the following example where a @var{z}-basis
ket is constructed and the @var{x}-basis ket is computed.

@example
@group
(%i1) declare([a,b],complex);
(%o1)                                done
(%i2) psi:mket([a,b]);
                                     [ a ]
(%o2)                                [   ]
                                     [ b ]
(%i3) psi_x:'xp*braket(xp,psi)+'xm*braket(xm,psi);
                    b         a              a         b
(%o3)           (------- + -------) xp + (------- - -------) xm
                 sqrt(2)   sqrt(2)        sqrt(2)   sqrt(2)
@end group
@end example

@subsection Pauli matrices and Sz, Sx, Sy operators

@deffn {Function} @code{sigmax}
Returns the Pauli @var{x} matrix.
@end deffn

@deffn {Function} @code{sigmay}
Returns the Pauli @var{y} matrix.
@end deffn

@deffn {Function} @code{sigmaz}
Returns the Pauli @var{z} matrix.
@end deffn

@deffn {Function} @code{Sx}
Returns the spin-1/2 @var{Sx} matrix.
@end deffn

@deffn {Function} @code{Sy}
Returns the spin-1/2 @var{Sy} matrix.
@end deffn

@deffn {Function} @code{Sz}
Returns the spin-1/2 @var{Sz} matrix.
@end deffn

@example
@group
(%i1) sigmay;
                                 [ 0   - %i ]
(%o1)                            [          ]
                                 [ %i   0   ]
(%i2) Sy;
                            [            %i hbar ]
                            [    0     - ------- ]
                            [               2    ]
(%o2)                       [                    ]
                            [ %i hbar            ]
                            [ -------      0     ]
                            [    2               ]
@end group
@end example

@deffn {Function} commutator (@code{X,Y})
Given two operators @code{X} and @code{Y}, return the
commutator @code{X . Y - Y . X}.
@end deffn

@example
@group
(%i1) commutator(Sx,Sy);
                           [        2             ]
                           [ %i hbar              ]
                           [ --------      0      ]
                           [    2                 ]
(%o1)                      [                      ]
                           [                    2 ]
                           [             %i hbar  ]
                           [    0      - -------- ]
                           [                2     ]
@end group
@end example

@deffn {Function} anticommutator (@code{X,Y})
Given two operators @code{X} and @code{Y}, return the
commutator @code{X . Y + Y . X}.
@end deffn

@example
@group
(%i1) (1/2)*anticommutator(sigmax,sigmax);
                                   [ 1  0 ]
(%o1)                              [      ]
                                   [ 0  1 ]
@end group
@end example

@subsection SX, SY, SZ operators for any spin

@deffn {Function} SX (@code{s})
@code{SX(s)} for spin @code{s} returns the matrix representation of the
spin operator @code{Sx}. Shortcuts for spin-1/2 are @code{Sx,Sy,Sz}, and
for spin-1 are @code{Sx1,Sy1,Sz1}.
@end deffn

@deffn {Function} SY (@code{s})
@code{SY(s)} for spin @code{s} returns the matrix representation of the
spin operator @code{Sy}. Shortcuts for spin-1/2 are @code{Sx,Sy,Sz}, and
for spin-1 are @code{Sx1,Sy1,Sz1}.
@end deffn

@deffn {Function} SZ (@code{s})
@code{SZ(s)} for spin @code{s} returns the matrix representation of the
spin operator @code{Sz}. Shortcuts for spin-1/2 are @code{Sx,Sy,Sz}, and
for spin-1 are @code{Sx1,Sy1,Sz1}.
@end deffn

Example:

@example
@group
(%i1) SY(1/2);
                            [            %i hbar ]
                            [    0     - ------- ]
                            [               2    ]
(%o1)                       [                    ]
                            [ %i hbar            ]
                            [ -------      0     ]
                            [    2               ]
(%i2) SX(1);
                         [           hbar            ]
                         [    0     -------     0    ]
                         [          sqrt(2)          ]
                         [                           ]
                         [  hbar              hbar   ]
(%o2)                    [ -------     0     ------- ]
                         [ sqrt(2)           sqrt(2) ]
                         [                           ]
                         [           hbar            ]
                         [    0     -------     0    ]
                         [          sqrt(2)          ]
@end group
@end example

@subsection Expectation value and variance

@deffn {Function} expect (@code{O,psi})
Computes the quantum mechanical expectation value of the operator @code{O}
in state @code{psi}, @code{<psi|O|psi>}.
@end deffn

@example
@group
(%i1) ev(expect(Sy,xp+ym),ratsimp);
(%o1)                               - hbar
@end group
@end example

@deffn {Function} qm_variance (@code{O,psi})
Computes the quantum mechanical variance of the operator @code{O}
in state @code{psi}, @code{sqrt(<psi|O@sup{2}|psi> - <psi|O|psi>@sup{2})}.
@end deffn

@example
@group
(%i1) ev(qm_variance(Sy,xp+ym),ratsimp);
                                    %i hbar
(%o1)                               -------
                                       2
@end group
@end example

@subsection Angular momentum representation of kets and bras

@subsubsection Matrix representation of (j,m)-kets and bras

The matrix representation of kets and bras in the @code{qm} package are
represented in the @code{z}-basis. To create a matrix representation of
of a ket or bra in the (j,m)-basis one uses the @code{spin_mket} and
@code{spin_mbra} functions.

@deffn {Function} spin_mket (s,m@sub{s},[1,2])
@code{spin_mket} returns a ket in the @code{z}-basis for spin @code{s}
and z-projection @code{m@sub{s}}, for axis 1=X or 2=Y.
@end deffn

@deffn {Function} spin_mbra (s,m@sub{s},[1,2])
@code{spin_mbra} returns a bra in the @code{z}-basis for spin @code{s}
and z-projection @code{m@sub{s}}, for axis 1=X or 2=Y.
@end deffn

@example
@group
(%i1) spin_mket(3/2,1/2,2);
                                [  sqrt(3)   ]
                                [  -------   ]
                                [    3/2     ]
                                [   2        ]
                                [            ]
                                [     %i     ]
                                [    ----    ]
                                [     3/2    ]
                                [    2       ]
(%o1)                           [            ]
                                [     1      ]
                                [    ----    ]
                                [     3/2    ]
                                [    2       ]
                                [            ]
                                [ sqrt(3) %i ]
                                [ ---------- ]
                                [     3/2    ]
                                [    2       ]
(%i2) spin_mbra(1,1,1);
                               [ 1     1     1 ]
(%o2)                          [ -  -------  - ]
                               [ 2  sqrt(2)  2 ]
@end group
@end example

@subsubsection Abstract (j,m)-kets and bras

To create kets and bras in the @var{|j,m>} representation you use the
abstract @code{ket} and @code{bra} functions with @code{j,m} as
arguments, as in @code{ket([j,m])} and @code{bra([j,m])}.

@example
@group
(%i1) bra([3/2,1/2]);
                                     3  1
(%o1)                               <-, -|
                                     2  2
(%i2) ket([3/2,1/2]);
                                     3  1
(%o2)                               |-, ->
                                     2  2
@end group
@end example

@deffn {Function} jmketp (@emph{jmket})
@code{jmketp} checks to see that the ket has an @code{m}-value that is in
the set @code{@{-j,-j+1,@dots{},+j@}}.
@end deffn

@example
@group
(%i1) jmketp(ket([j,m]));
(%o1)                                false
(%i2) jmketp(ket([3/2,1/2]));
(%o2)                                true
@end group
@end example


@deffn {Function} jmbrap (@emph{jmbra})
@code{jmbrap} checks to see that the bra has an @code{m}-value that is in
the set @code{@{-j,-j+1,@dots{},+j@}}.
@end deffn

@deffn {Function} jmcheck (@code{j,m})
@code{jmcheck} checks to see that @var{m} is one of @{-j, @dots{}, +j@}.
@end deffn

@example
@group
(%i1) jmcheck(3/2,1/2);
(%o1)                                true
@end group
@end example

@deffn {Function} JP (@emph{jmket})
@code{JP} is the @code{J@sub{+}} operator. It takes a @code{jmket}
@code{jmket(j,m)} and returns @code{sqrt(j*(j+1)-m*(m+1))*hbar*jmket(j,m+1)}.
@end deffn

@deffn {Function} JM (@emph{jmket})
@code{JM} is the @code{J@sub{-}} operator. It takes a @code{jmket}
@code{jmket(j,m)} and returns @code{sqrt(j*(j+1)-m*(m-1))*hbar*jmket(j,m-1)}.
@end deffn

@deffn {Function} Jsqr (@emph{jmket})
@code{Jsqr} is the @code{J@sup{2}} operator. It takes a @code{jmket}
@code{jmket(j,m)} and returns @code{(j*(j+1)*hbar@sup{2}*jmket(j,m)}.
@end deffn

@deffn {Function} Jz (@emph{jmket})
@code{Jz} is the @code{J@sub{z}} operator. It takes a @code{jmket}
@code{jmket(j,m)} and returns @code{m*hbar*jmket(j,m)}.
@end deffn

These functions are illustrated below.

@example
@group
(%i1) k:ket([j,m]);
(%o1)                               |j, m>
(%i2) JP(k);
(%o2)             hbar |j, m + 1> sqrt(j (j + 1) - m (m + 1))
(%i3) JM(k);
(%o3)             hbar |j, m - 1> sqrt(j (j + 1) - (m - 1) m)
(%i4) Jsqr(k);
                                2
(%o4)                       hbar  j (j + 1) |j, m>
(%i5) Jz(k);
(%o5)                            hbar |j, m> m
@end group
@end example

@subsection Addition of angular momentum in the (j,m)-representation

Addition of angular momentum calculations can be performed in the
(j,m)-representation using the function definitions below. The internal
representation of kets and bras for this purpose is the following. Given
kets @code{|j1,m1>} and @code{|j2,m2>} a tensor product of (j,m)-kets
is instantiated as:

@ @ @ @ @ @ @ @ @ @ @code{[tpket,1,|j1,m1>,|j2,m2>]}

and the corresponding bra is instantiated as:

@ @ @ @ @ @ @ @ @ @ @code{[tpket,1,<j1,m1|,<j2,m2|]}

where the factor of 1 is the multiplicative factor of the tensor product.

Using the function definitions below one must be careful to avoid errors
produced by Maxima's automatic list arithmetic. For example, do not
use @code{(J1z+J2z)}, and instead use the defined function @code{Jtz}.
Similarly for any of the operators that are added together, one should
always use the total @code{Jtxx} defined function.

@deffn {Function} tpket (@emph{jmket1,jmket2})
@code{tpket} instantiates a tensor product of two (j,m)-kets.
@end deffn

@example
@group
(%i1) tpket(ket([3/2,1/2]),ket([1/2,1/2]));
                                      3  1    1  1
(%o1)                     [tpket, 1, |-, ->, |-, ->]
                                      2  2    2  2
@end group
@end example

@deffn {Function} tpbra (@emph{jmbra1,jmbra2})
@code{tpbra} instantiates a tensor product of two (j,m)-bras.
@end deffn

@example
@group
(%i1) tpbra(bra([3/2,1/2]),bra([1/2,1/2]));
                                      3  1    1  1
(%o1)                     [tpbra, 1, <-, -|, <-, -|]
                                      2  2    2  2
@end group
@end example

@deffn {Function} tpbraket (@emph{tpbra,tpket})
@code{tpbraket} returns the bracket of a @code{tpbra} and a @code{tpket}.
@end deffn

@deffn {Function} J1z (@emph{tpket})
@code{J1z} returns the tensor product of a tpket with @code{Jz} acting
on the first ket.
@end deffn

@deffn {Function} J2z (@emph{tpket})
@code{J2z} returns the tensor product of a tpket with @code{Jz} acting
on the second ket.
@end deffn

@example
@group
(%i1) k:tpket(ket([3/2,3/2]),ket([1/2,1/2]));
                                      3  3    1  1
(%o1)                     [tpket, 1, |-, ->, |-, ->]
                                      2  2    2  2
(%i2) J1z(k);
                                3 hbar   3  3    1  1
(%o2)                   [tpket, ------, |-, ->, |-, ->]
                                  2      2  2    2  2
(%i3) J2z(k);
                                 hbar   3  3    1  1
(%o3)                    [tpket, ----, |-, ->, |-, ->]
                                  2     2  2    2  2
@end group
@end example

@deffn {Function} Jtz (@emph{tpket})
@code{Jtz} is the total z-projection of spin operator acting on a tpket
and returning @code{(J@sub{1z}+J@sub{2z})}.
@end deffn

@example
@group
(%i1) k:tpket(ket([3/2,3/2]),ket([1/2,1/2]));
                                      3  3    1  1
(%o1)                     [tpket, 1, |-, ->, |-, ->]
                                      2  2    2  2
(%i2) Jtz(k);
                                         3  3    1  1
(%o2)                   [tpket, 2 hbar, |-, ->, |-, ->]
                                         2  2    2  2
@end group
@end example


@deffn {Function} J1sqr (@emph{tpket})
@code{J1sqr} returns @code{Jsqr} for the first ket of a tpket.
@end deffn

@deffn {Function} J2sqr (@emph{tpket})
@code{J2sqr} returns @code{Jsqr} for the second ket of a tpket.
@end deffn

@deffn {Function} J1p (@emph{tpket})
@code{J1p} returns @code{J@sub{+}} for the first ket of a tpket.
@end deffn

@deffn {Function} J2p (@emph{tpket})
@code{J2p} returns @code{J@sub{+}} for the second ket of a tpket.
@end deffn

@deffn {Function} Jtp (@emph{tpket})
@code{Jtp} returns @code{(J@sub{1+}+J@sub{2+})} for the tpket.
@end deffn

@deffn {Function} J1m (@emph{tpket})
@code{J1m} returns @code{J@sub{-}} for the first ket of a tpket.
@end deffn

@deffn {Function} J2m (@emph{tpket})
@code{J2m} returns @code{J@sub{=}} for the second ket of a tpket.
@end deffn

@deffn {Function} Jtm (@emph{tpket})
@code{Jtm} returns @code{(J@sub{1-}+J@sub{2-})} for the tpket.
@end deffn

@deffn {Function} J1p2m (@emph{tpket})
@code{J1p2m} returns @code{(J@sub{1+}J@sub{2-})} for the tpket.
@end deffn

@deffn {Function} J1m2p (@emph{tpket})
@code{J1m2p} returns @code{(J@sub{1-}J@sub{2+})} for the tpket.
@end deffn

@deffn {Function} J1zJ2z (@emph{tpket})
@code{J1zJ2z} returns @code{(J@sub{1z}J@sub{2z})} for the tpket.
@end deffn

@deffn {Function} Jtsqr (@emph{tpket})
@code{Jtsqr} returns @code{(J@sub{1}@sup{2}+J@sub{2}@sup{2}+
J@sub{1+}J@sub{2-}+J@sub{1-}J@sub{2+}+J@sub{1z}J@sub{2z})} for the
tpket.
@end deffn

@example
@group
(%i1) k:tpket(ket([3/2,1/2]),ket([1/2,1/2]));
                                      3  1    1  1
(%o1)                     [tpket, 1, |-, ->, |-, ->]
                                      2  2    2  2
(%i2) b:dagger(k);
                                      3  1    1  1
(%o2)                     [tpbra, 1, <-, -|, <-, -|]
                                      2  2    2  2
(%i3) J1p2m(k);
                                       2   3  3    1    1
(%o3)              [tpket, sqrt(3) hbar , |-, ->, |-, - ->]
                                           2  2    2    2
(%i4) J1m2p(k);
(%o4)                                  0
@end group
@group
(%i1) k:tpket(ket([3/2,-1/2]),ket([1/2,1/2]));
                                     3    1    1  1
(%o1)                    [tpket, 1, |-, - ->, |-, ->]
                                     2    2    2  2
(%i2) J1zJ2z(k);
                                    2
                                hbar    3    1    1  1
(%o2)                 [tpket, - -----, |-, - ->, |-, ->]
                                  4     2    2    2  2
(%i3) Jtsqr(k);
                    2   3    1    1  1                   2   3  1    1    1
(%o3) [tpket, 4 hbar , |-, - ->, |-, ->] + [tpket, 2 hbar , |-, ->, |-, - ->]
                        2    2    2  2                       2  2    2    2
@end group
@end example

@subsection Angular momentum and ladder operators

@deffn {Function} SP (@code{s})
@code{SP} is the raising ladder operator @var{S@sub{+}} for spin @code{s}.
@end deffn

@deffn {Function} SM (@code{s})
@code{SM} is the raising ladder operator @var{S@sub{-}} for spin @code{s}.
@end deffn

Examples of the ladder operators:

@example
@group
(%i1) SP(1);
                       [ 0  sqrt(2) hbar       0       ]
                       [                               ]
(%o1)                  [ 0       0        sqrt(2) hbar ]
                       [                               ]
                       [ 0       0             0       ]
(%i2) SM(1);
                       [      0             0        0 ]
                       [                               ]
(%o2)                  [ sqrt(2) hbar       0        0 ]
                       [                               ]
                       [      0        sqrt(2) hbar  0 ]
@end group
@end example

@section Rotation operators

@deffn {Function} RX (@code{s,t})
@code{RX(s)} for spin @code{s} returns the matrix representation of the
rotation operator @code{Rx} for rotation through angle @code{t}.
@end deffn

@deffn {Function} RY (@code{s,t})
@code{RY(s)} for spin @code{s} returns the matrix representation of the
rotation operator @code{Ry} for rotation through angle @code{t}.
@end deffn

@deffn {Function} RZ (@code{s,t})
@code{RZ(s)} for spin @code{s} returns the matrix representation of the
rotation operator @code{Rz} for rotation through angle @code{t}.
@end deffn

@example
@group
(%i1) RY(1,t);
Proviso: assuming 4*t # 0 
                     [ cos(t) + 1    sin(t)   1 - cos(t) ]
                     [ ----------  - -------  ---------- ]
                     [     2         sqrt(2)      2      ]
                     [                                   ]
                     [  sin(t)                  sin(t)   ]
(%o1)                [  -------     cos(t)    - -------  ]
                     [  sqrt(2)                 sqrt(2)  ]
                     [                                   ]
                     [ 1 - cos(t)   sin(t)    cos(t) + 1 ]
                     [ ----------   -------   ---------- ]
                     [     2        sqrt(2)       2      ]
@end group
@end example

@section Time-evolution operator

@deffn {Function} UU (@code{H,t})
@code{UU(H,t)} is the time evolution operator for Hamiltonian @code{H}. It
is defined as the matrix exponential @code{matrixexp(-%i*H*t/hbar)}.
@end deffn

@example
@group
(%i1) UU(w*Sy,t);
Proviso: assuming 64*t*w # 0 
                           [     t w         t w  ]
                           [ cos(---)  - sin(---) ]
                           [      2           2   ]
(%o1)                      [                      ]
                           [     t w        t w   ]
                           [ sin(---)   cos(---)  ]
                           [      2          2    ]
@end group
@end example

@section Tensor products

Tensor products are represented as lists in the @code{qm} package. The
ket tensor product @code{|z+,z+>} can be represented as
@code{ket([u,d])}, for example, and the bra tensor product @code{<a,b|}
is represented as @code{bra([a,b])} for states @code{a} and
@code{b}. For a tensor product where the identity is one of the elements
of the product, substitute the string @code{Id} in the ket or bra at the
desired location. See the examples below for the use of the identity in
tensor products.

Examples below show how to create abstract tensor products that contain
the identity element @code{Id} and how to take the bracket of these
tensor products.

@example
@group
(%i1) K:ket([a1,b1]);
(%o1)                              |a1, b1>
(%i2) B:bra([a2,b2]);
(%o2)                              <a2, b2|
(%i3) braket(B,K);
(%o3)                kron_delta(a1, a2) kron_delta(b1, b2)
@end group
@group
(%i1) bra([a1,Id,c1]) . ket([a2,b2,c2]);
(%o1)          |-, b2, -> kron_delta(a1, a2) kron_delta(c1, c2)
(%i2) bra([a1,b1,c1]) . ket([Id,b2,c2]);
(%o2)          <a1, -, -| kron_delta(b1, b2) kron_delta(c1, c2)
@end group
@end example

In the next example we construct the state function for an entangled
Bell pair, construct the density matrix, and then trace over the first
particle to obtain the density submatrix for particle 2.

@example
@group
(%i1) bell:(1/sqrt(2))*(ket([u,d])-ket([d,u]));
                                |u, d> - |d, u>
(%o1)                           ---------------
                                    sqrt(2)
(%i2) rho:bell . dagger(bell);
      |u, d> . <u, d| - |u, d> . <d, u| - |d, u> . <u, d| + |d, u> . <d, u|
(%o2) ---------------------------------------------------------------------
                                        2
(%i3) assume(not equal(u,d));
(%o3)                          [notequal(u, d)]
(%i4) trace1:bra([u,Id]) . rho . ket([u,Id])+bra([d,Id]) . rho . ket([d,Id]);
                       |-, u> . <-, u|   |-, d> . <-, d|
(%o4)                  --------------- + ---------------
                              2                 2
@end group
@end example

@subsection Quantum harmonic oscillator

The @code{qm} package can perform simple quantum harmonic oscillator
calculations involving the ladder operators @code{a@sup{+}} and
@code{a@sup{-}}. These are referred to in the package as @code{ap} and
@code{am} respectively. To do the following examples you must load the
@code{qho} package that is included in @code{qm} with @code{load(qho)}.

A common problem is to compute the 1st order change in energy of a state
due to a perturbation of the harmonic potential, say an additional
factor @code{V(x) = x^2 + g*x^4} for small @code{g}. This example is
performed below, ignoring any physical constants in the problem.

@example
@group
(%i1) load(qho);
(%o1)          /home/ehm/math/Maxima/share/ehm/qm-maxima/qho.mac
(%i2) declare(n,integer,n,scalar);
(%o2)                                done
(%i3) ap . ket([n]);
(%o3)                         sqrt(n + 1) |n + 1>
(%i4) am . ket([n]);
(%o4)                           |n - 1> sqrt(n)
(%i5) bra([n]) . (ap+am)^^4 . ket([n]);
                                   2
(%o5)                           6 n  + 6 n + 3
@end group
@end example

@*
Another package that handles quantum mechanical operators is
@code{operator_algebra} written by Barton Willis.

@node Function and Variable Index, , Top, Top
@appendix Function and Variable index
@printindex fn
@printindex vr

@bye
